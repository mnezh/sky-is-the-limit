plugins {
    id 'java'
    id 'com.diffplug.spotless' version "${spotlessVersion}"
}

repositories {
    mavenCentral()
}

dependencies {
    // Cucumber
    testImplementation "io.cucumber:cucumber-java:${cucumberVersion}"
    testImplementation "io.cucumber:cucumber-junit-platform-engine:${cucumberVersion}"
    testImplementation "io.cucumber:cucumber-picocontainer:${cucumberVersion}"

    // JUnit
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testImplementation "org.junit.platform:junit-platform-suite:${junitVersion}"

    // RestAssured
    testImplementation "io.rest-assured:rest-assured:${restAssuredVersion}"

    // Jackson for JSON serialization/deserialization
    testImplementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    testImplementation "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:${jacksonVersion}"
}

spotless {
    java {
        googleJavaFormat()
        endWithNewline()
    }
}

test {
    def whitelist = ['base.url', 'username', 'password']
    project.properties.each { key, value ->
        if (whitelist.contains(key)) {
            systemProperty(key, value)
        }
    }
    if (project.hasProperty("tags")) {
        systemProperty "cucumber.filter.tags", project.getProperty("tags")
    }

    def reportPath = project.hasProperty('report.path') ? project.getProperty('report.path') : 'cucumber-reports'

    useJUnitPlatform()
    doFirst {
        // Use the defined reportPath variable to create the directory
        file("${reportPath}/cucumber").mkdirs()
    }

    systemProperty "cucumber.plugin", "pretty, summary, html:${reportPath}/cucumber/cucumber-report.html"

    systemProperty "cucumber.publish.quiet", "true"
    systemProperty "cucumber.execution.strict", "true"
    systemProperty "cucumber.snippet-type", "camelcase"

    testLogging {
        showStandardStreams = false
        exceptionFormat = 'full'
        events "passed", "skipped", "failed", "standardError"
    }
}